#!/usr/bin/perl
use strict;
use warnings;
use Data::Dumper;

my @files = split /\s+/,
 'he3_const.f he3_phase.f he3_fermi.f he3_gap.f ' .
 'he3_dipole.f he3_grad.f he3_text.f ' .
 'he3_transp_n.f he3_transp_b.f ' .
 'he3_b2.f he3_other.f he3_normal.f he3_a.f he3_polar.f ' .
 'he3_rota.f he3_bspec.f he3_math.f he4.f he34.f he3_wire.f'
;

# quote text for putting it into c-string
sub quote_c_str($){
  my $arg=shift || '';
  $arg =~ s/\"/\\\"/g;
  $arg =~ s/\\/\\\\/g;
  return $arg;
}

# quote text for putting it into c-comment
sub quote_c_comm($){
  my $arg=shift || '';
  $arg =~ s|\*/|\*|g;
  return $arg;
}


#############################################
# Read source files, extract function profiles,
# comments, etc.
#
# Comments should start with '!>'.
# They will be attached to the following function (if any)
# or become a block of text.
my @data;
my @dblocks;
foreach my $fname (@files) {
  open F, "functions/$fname" or die "can't open $fname: $!";
  my $mode = 0;
  my $text = '';
  push @data, {
    type=>'file',
    fname=>$fname
  };
  foreach (<F>){

    # lines with text, !> comments
    if (/^\!>\s*(.*)/){
      $text .= "\n" if $text;
      $text .= $1;
      next;
    }

    # functions
    elsif (/^\s*function\s+(\w+)\(([^\)]+)\)\s*\!F>\s*(.*)/){
      my $name = $1;
      my $args = $2;
      my $title = $3;
      if (!$title){
        $title = $text;
        $title =~ s/\n.*//s;
        $text =~ s/.*\n//s;
      }
      $args =~ s/\s+//g;
      my $narg = scalar(split ',', $args);
      push @data, {
        type=>'func',
        name=>$name,
        args=>$args,
        narg=>$narg,
        title=>$title,
        text=>$text,
        fname=>$fname
      };
    }

    # constants
    elsif (/^[ .]+(\w+)\s*\/([0-9D.+-]+)\/,?\s*!C>\s*(.*)/){
      my $name = $1;
      my $value = $2;
      my $title = $3;
      $value =~ s/D0$//i;
      $value =~ s/D0+/D/i;
      $value =~ s/D/e/i;
      push @data, {
        type=>'const',
        name=>$name,
        args=>'',
        narg=>0,
        value=>$value,
        title=>$title,
        text=>$text,
        fname=>$fname
      };
    }

    # headers
    elsif (/^\!H>\s*(.*)/){
      push @data, {
        type=>"head",
        text=>$1
      };
    }

    # data blocks
    elsif (/^\s*block\s*data\s+(\w+)/){
      push @dblocks, $1;
    }

    elsif ($text) {
      push @data, {
        type=>'text',
        text=>$text
      };
    }

    $text = '';
  }
  close F;
}


#############################################
# write header files

open(OUT_C,   '> he3.h')    or die 'Can not open he3.h: $!';
open(OUT_F77, '> he3.fh')   or die 'Can not open he3.fh: $!';
open(OUT_F90, '> he3.f90h') or die 'Can not open he3.f90h: $!';
open(OUT_TAB, '> he3tab.h') or die 'Can not open he3tab.h: $!';

my $head = "This file is generated by $0 script. Don't modify it!";
print OUT_C   "/* $head */\n\n";
print OUT_TAB "/* $head */\n\n";
print OUT_F77 "! $head\n\n";
print OUT_F90 "! $head\n\n";

print OUT_C   "#ifndef HE3_H\n#define HE3_H\n";

print OUT_TAB qq|
#include "he3.h"

typedef double (*fun1_t)(double*);
typedef double (*fun2_t)(double*, double*);
typedef double (*fun3_t)(double*, double*, double*);
typedef double (*fun4_t)(double*, double*, double*, double*);
typedef double (*fun5_t)(double*, double*, double*, double*, double*);
typedef double (*fun6_t)(double*, double*, double*, double*, double*, double*);
typedef double (*fun7_t)(double*, double*, double*, double*, double*, double*, double*);
typedef double (*fun8_t)(double*, double*, double*, double*, double*, double*, double*, double*);

struct tab_t {
  const char * name;
  const void * func;
  const int    narg;
  const char * args;
  const char * comm;
};
struct tab_t func_tab[] = {
|;

# This is the only difference between F77 and F90 header files.
# We need this to avoid possible linker problems
# See http://gcc.gnu.org/onlinedocs/gcc-3.4.6/g77/Block-Data-and-Libraries.html#Block-Data-and-Libraries
print OUT_F90 "external $_\n" foreach (@dblocks);

# NaN definition for fortran
my $fnan =qq{
!NaN parameter. -fno-range-check should be used in compilation!
      real*8 NaN
      parameter (NaN = 0D0/0D0)
};
print OUT_F77 $fnan;
print OUT_F90 $fnan;

my $cmn = "\n! Common blocks for constants\n";

foreach my $d (@data) {

  if ($d->{type} eq 'file') {
    print OUT_C   "\n/", '*'x50, "/\n/* Source file: $d->{fname} */\n";
    print OUT_F77 "\n!", '-'x50, "\n! Source file: $d->{fname}\n";
    print OUT_F90 "\n!", '-'x50, "\n! Source file: $d->{fname}\n";
  }

  if ($d->{type} eq 'head') {
    printf OUT_C  "\n/* %s */\n", quote_c_comm($d->{text});
    print OUT_F77 "\n! $d->{text}\n";
    print OUT_F90 "\n! $d->{text}\n";
  }

  if ($d->{type} eq 'func') {
    my @args = split(/\s*,\s*/, $d->{args});
    my $cargs .= 'double *' . join(', double *', @args);
    print OUT_C   "double $d->{name}_($cargs);\n";
  }

  if ($d->{type} eq 'const') {
    printf OUT_C   "extern double $d->{name}_;\n";
    $cmn .= sprintf "      common %17s $d->{name}\n", "/$d->{name}/";
  }

  if ($d->{type} eq 'func' || $d->{type} eq 'const') {
    print  OUT_F77 "      real*8 $d->{name}\n";
    print  OUT_F90 "      real*8 $d->{name}\n";

    # create octave wrapper functions
    open(OCT_F, "> m/$d->{name}.m") or die "can't open m/$d->{name}.m: $!";
    print OCT_F "function v=$d->{name}(varargin) v=he3lib(\"$d->{name}\", varargin{:}); end";
    close(OCT_F);

    # TAB-file
    my $tcomm = $d->{title} || '';
    $tcomm =~ s/\"/\\\"/g;
    printf OUT_TAB "{%-22s %-36s, %d, \"%s\", \"%s\"},\n",
      "\"$d->{name}\",", "(const void*)&$d->{name}_",
      $d->{narg}, $d->{args}, quote_c_str($d->{title});
  }
}

print OUT_F77 $cmn, "\n";
print OUT_F90 $cmn, "\n";

print OUT_TAB "{NULL,NULL,0,NULL,NULL}};\n";
print OUT_C "#endif\n";
close OUT_F77;
close OUT_F90;
close OUT_TAB;

#############################################
# write documentation

__END__
my $newfile = -1;
foreach my $d (@data) {

  ## new file
  if ($d->{type} eq 'file'){
    print "> $d->{fname}\n";
    $newfile = 1;
    next;
  }

  ## text
  if ($d->{type} eq 'file'){
      print "$d->{text}\n";
  }

  ## function
  elsif ($d->{type} eq 'func'){
    print "$d->{name}($d->{args}) -- $d->{title}\n";
    print "  <<$_>>\n" foreach (split "\n", $d->{text});
    print "\n";
  }

  ## constant
  elsif ($d->{type} eq 'const'){
    print "$d->{name} -- $d->{value} -- $d->{title}\n";
  }

  # data array should start with a new file
  die "new file expected" if $newfile < 0;
  $newfile = 0;
}


